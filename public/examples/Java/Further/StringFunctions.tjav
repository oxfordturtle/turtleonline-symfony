class StringFunctions {
  void main () {
    final int delay = 500;
    output(true, cream, true);
    // set initial String
    String s1 = 'Turtle C';
    // illustrate use of STRCPY function ...
    String s2 = strcpy(s1, 1, 6);
    writeline(s2 + ' is the name of a small animal.');
    pause(delay);
    String s3 = strcpy(s1, 8, 6);
    writeline('\'' + s3 + '\' is the third letter of the alphabet.');
    pause(delay);
    // illustrate use of STRINS function ...
    String s4 = strins(s3, '++', 2);
    writeline(s4 + ' is an extension of the C programming language.');
    pause(delay);
    // illustrate use of STRLEN function ...
    writeline('"' + s1 + '" has ' + toString(strlen(s1)) + ' characters.');
    pause(delay);
    // illustrate use of STRLWR and STRUPR functions ...
    writeline('In lower case it is "' + strlwr(s1) + '".');
    writeline('In upper case it is "' + strupr(s1) + '".');
    pause(delay);
    // illustrate use of STRPOS function ...
    int posn = strpos(s1, s3);
    if (posn > 0) {
      writeline('"' + s3 + '" occurs within "' + s1 + '" at position ' + toString(posn) + '.');
    }
    pause(delay);
    // illustrate use of QVAL function, converting a decimal String to an integer ...
    String s5 = '3.14159';
    writeline(s5 + ' times 100000 = ' + toString(qval(s5, 100000, -1)) + '.');
    pause(delay);
    int n = qval(s5, 100000, -1);
    writeline(toString(n) + ' divided by 100000 = ' + qstr(n, 100000, 5) + '.');
    writeline('');
    pause(delay * 5);
    // illustrate use of OUTPUT and CONSOLE commands ...
    writeline('Now back to the Canvas and Console ...');
    output(false, peach, true);
    pause(delay * 5);
    output(false, lightred, false);
    pause(delay * 5);
    console(true, lightblue);
    writeline('You will see this on a clear light blue Console');
  }
}
