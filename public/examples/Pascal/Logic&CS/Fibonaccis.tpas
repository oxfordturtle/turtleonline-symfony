PROGRAM Fibonaccis;
{specify number of values to be calculated}
CONST lastnum = 25;
VAR count: integer;
    fibSave: array[1..lastnum] of integer;

  {calculates nth Fibonacci number recursively}
  Function fibRecursive(n: integer): integer;
  Begin
    if (n = 1) or (n = 2) then
      result := 1
    else
      result := fibRecursive(n - 1) + fibRecursive(n - 2)
  End;

  {calculates nth Fibonacci number iteratively,}
  {assuming the n-1 and n-2 values have already}
  {been calculated and stored in fibSave array }
  Function fibIterative(n: integer): integer;
  Begin
    if (n = 1) or (n = 2) then
      fibSave[n] := 1
    else
      fibSave[n] := fibSave[n - 1] + fibSave[n - 2];
    result := fibSave[n]
  End;

BEGIN
  {set up and show the output display}
  output(true, cream, true);
  {calculate 25 Fibonacci values recursively (slow)}
  writeln('First, by recursion:');
  timeset(0);
  for count := 1 to lastnum do
    writeln('fib(' + str(count) + ') = ' + str(fibRecursive(count)));
  writeln('Time taken: ' + qstr(time, 1000, 2) + ' seconds');
  writeln('');
  writeln('Then, by iteration:');
  timeset(0);
  for count := 1 to lastnum do
    writeln('fib(' + str(count) + ') = ' + str(fibIterative(count)));
  writeln('Time taken: ' + qstr(time, 1000, 2) + ' seconds')
END.
