PROGRAM Arcade;
CONST targets = 10;
VAR faceX, faceY: integer;
    faceXspeed, faceYspeed: integer;
    hits, i: integer;

  {draws a bottle at the given coordinates}
  Procedure bottle(x, y: integer);
  Begin
    setxy(x, y);
    colour(cyan);
    movexy(0, -15);
    thickness(40);
    drawxy(0, -50);
    thickness(10);
    drawxy(0, -40);
    thickness(2);
    movexy(-20, 90);
    movexy(40, 0);
    movexy(0, -20);
    movexy(-40, 0);
    colour(purple);
    polygon(4);
    movexy(20, 35)
  End;

  {draws the scene background}
  Procedure background;
  Begin
    blank(coffee);
    bottle(100, 500);
    bottle(200, 500);
    bottle(450, 500);
    bottle(500, 500);
    bottle(565, 500);
    bottle(865, 500);
    colour(darkbrown);
    thickness(20);
    setxy(0, 125);
    drawxy(1000, 0);
    setxy(0, 325);
    drawxy(1000, 0);
    setxy(0, 525);
    drawxy(1000, 0);
    colour(brown);
    thickness(30);
    setxy(0, 100);
    drawxy(1000, 0);
    setxy(0, 300);
    drawxy(1000, 0);
    setxy(0, 500);
    drawxy(1000, 0)
  End;

  {draws the scene foreground}
  Procedure foreground;
  Begin
    colour(brown);
    thickness(272);
    setxy(0, 863);
    drawxy(1000, 0);
  End;

  {draws a face with a hat}
  Procedure face;

    {draws a hat}
    Procedure hat;
    Begin
      setxy(faceX, faceY - 80);
      colour(darkgrey);
      ellblot(90, 30);
      thickness(2);
      colour(black);
      ellipse(90, 30);
      movexy(-70, 0);
      thickness(4);
      drawxy(35, -70);
      drawxy(70, 0);
      drawxy(35, 70);
      colour(darkgrey);
      thickness(1);
      polygon(4)
    End;

  Begin
    thickness(1);
    colour($FF8899);
    blot(100);
    colour(white);
    setxy(faceX - 20, faceY - 20);
    blot(10);
    colour(black);
    blot(4);
    thickness(4);
    movexy(0, -22);
    colour($663300);
    drawxy(15, 15);
    thickness(1);
    colour(white);
    setxy(faceX + 20, faceY - 20);
    blot(10);
    colour(black);
    blot(4);
    thickness(4);
    movexy(0, -22);
    colour($663300);
    drawxy(-15, 15);
    hat;
    setxy(faceX, faceY + 13);
    colour(red);
    blot(10);
    movexy(0, 32);
    ellblot(30, 6);
    colour(white);
    ellblot(20, 4)
  End;

  {draws black crosshairs}
  Procedure gunsight;
  Begin
    colour(black);
    thickness(6);
    setxy(?mousex - 50, ?mousey);
    drawxy(100, 0);
    setxy(?mousex, ?mousey - 50);
    drawxy(0, 100);
    thickness(2);
  End;

BEGIN
  {set initial face coordinates and speed}
  faceX := -100;
  faceY := 625;
  faceXspeed := 5;
  faceYspeed := 0;
  {initialise hits to zero}
  hits := 0;
  {hide default cursor}
  cursor(0);
  {until all targets have been hit...}
  while hits < targets do
    begin
      noupdate;
      {draw background}
      background;
      {draw face at current location}
      setxy(faceX, faceY);
      face;
      {draw one bottle for each target hit so far}
      for i := 1 to hits do
        bottle(50 * i - 35, 740);
      {draw foreground}
      foreground;
      {draw gunsight}
      gunsight;
      update;
      {move to next face position}
      faceX := faceX + faceXspeed;
      faceY := faceY + faceYspeed;
      {loop round at canvas edges}
      if faceX > 1100 then
        faceX := -100;
      if faceX < -100 then
        faceX := 1100;
      if (faceY < 100) or (faceY > 625) then
        faceYspeed := faceYspeed * -1;
      {if a mouse click is detected on the face...}
      if ((?lmouse > 0) and (((?mousex - faceX) * (?mousex - faceX)) + ((?mousey - faceY) * (?mousey - faceY)) < 10000)) then
        begin
          {create a new target}
          faceX := -100;
          faceXspeed := random(5) + 2;
          faceYspeed := random(9) - 4;
          faceY := random(550) + 50;
          {incremenet number of hits}
          inc(hits)
        end
    end;
  {print end of game message}
  blank(black);
  setxy(200, 450);
  colour(green);
  print('  Well done!  ', 4, 75)
END.
