PROGRAM MultipleTurtles;
CONST numTurtles = 5;
      x = 1;
      y = 2;
      d = 3;
      a = 4;
      t = 5;
      c = 6;
VAR turtles: array[1..numTurtles, x..c] of integer;
    i: integer;

  {set initial values for each turtle}
  Procedure setupTurtles;
  Var i: integer;
  Begin
    for i := 1 to numTurtles do
      begin
        turtles[i, x] := 10000;
        turtles[i, y] := 70000;
        turtles[i, d] := 35 + i * 10;
        turtles[i, a] := 360;
        turtles[i, t] := 5 + i;
        turtles[i, c] := rgb(i)
      end
  End;

  {draw menu (i.e. coloured blot for selecting each turtle)}
  Procedure menu;
  Var i: integer;
  Begin
    for i := 1 to numTurtles do
      begin
        setxy(5000, i * 10000);
        colour(rgb(i));
        blot(5000)
      end
  End;

  {handle a mouse click}
  Procedure handleClick;
  Var i, thisTurtle: integer;
  Begin
    {select turtle based on canvas colour at mouse location}
    thisTurtle := 0;
    for i := 1 to numTurtles do
      if pixcol(?mousex, ?mousey) = rgb(i) then
        thisTurtle := i;
    if thisTurtle > 0 then
      begin
        {select this turtle}
        newturtle(turtles[thisTurtle]);
        {start loggin the change in the console}
        write('Turtle ' + str(thisTurtle) + ' changing direction,');
        write('  from ' + str(turtles[thisTurtle, d]) + '/' + str(turtles[thisTurtle, a]));
        {change angles, which will set direction to the closest possible angle}
        angles(random(46) + 45);
        {finish logging the change}
        writeln('  to ' + str(turtles[thisTurtle, d]) + '/' + str(turtles[thisTurtle, a]));
        pause(200)
      end
  End;

BEGIN
  canvas(0, 0, 100000, 100000);
  {print instructions}
  setxy(2500, 80000);
  print('Click on a circle to alter the ANGLES setting', 0, 36);
  setxy(2500, 86000);
  print('of the corresponding turtle. Its direction will', 0, 36);
  setxy(2500, 92000);
  print('then change to the closest possible angle.', 0, 36);
  {draw initial paths for each turtle}
  for i := 1 to numTurtles do
    begin
      setxy(10000, 70000);
      direction(35 + i * 10);
      forward(110000)
    end;
  {draw the menu}
  menu;
  {initialise the turtles}
  setupTurtles;
  {repeatedly...}
  repeat
    {move each turtle forward}
    for i := 1 to numTurtles do
      begin
        {select this turtle}
        newturtle(turtles[i]);
        {move forward 100}
        forward(100)
      end;
    pause(50);
    {check for mouse click}
    if ?click > 128 then
      handleClick
  until ?key = \escape
END.
