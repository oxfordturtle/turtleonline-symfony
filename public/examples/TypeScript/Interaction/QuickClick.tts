blank(cream);
// print instructions
setXY(15, 200);
print('This reaction game will display a sequence', 4, 36);
setXY(20, 270);
print('of 10 coloured circles on the Canvas.  Try', 4, 36);
setXY(20, 340);
print('to click the mouse on each circle as quickly', 4, 36);
setXY(20, 410);
print('as you can, and see how short a total time', 4, 36);
setXY(20, 480);
print('you can achieve for all 10.', 4, 36);
setXY(100, 620);
print('Now press the "Esc" key to start ...', 4, 36);
keyEcho(false);
// wait for escape key before proceeding...
do {} while (abs(?key) != \escape);
// reset timer
timeSet(0);
for (var count: number = 1; count <= 10; count = count + 1) {
  // blank the screen
  blank(black);
  // draw a blot at random coordinates
  setXY(randInt(900) + 50, randInt(900) + 50);
  colour(rgb(count));
  blot(50);
  // until the blot has been clicked...
  do {
    // wait for left mouse click
    do {} while (?lmouse <= 0);
    // record the colour at the cursor's coordinates
    var clickColour: number = pixCol(?mousex, ?mousey);
  } while (clickColour != turtc);
}
// print time taken
blank(lightblue);
setXY(60, 460);
colour(lightred);
print('Your time was ' + toStringQ(time(), 1000, 2) + ' seconds', 4, 50);
