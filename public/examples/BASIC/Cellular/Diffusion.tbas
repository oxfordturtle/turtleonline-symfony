CONST WIDTH% = 100
CONST LEFTAXIS% = 21
CONST RIGHTAXIS% = 15
CONST TOPMARGIN% = 20
CONST GAP% = 10
CONST CONCBOTTOM% = 80
CONST CONCWIDTH% = 50
CONST COLOUR1% = RED
CONST COLOUR2% = BLUE
CONST EDGECOL% = BLACK
CONST MOLCOL% = RED
CONST CONCCOL% = SEAGREEN
DIM num1%(WIDTH%)

PROCsetup
PROCdrawaxes
FOR x1% = 0 TO WIDTH% - 1
  PROCgraphit(x1%, TRUE)
NEXT
PAUSE(2500)
REPEAT
  x1% = RND(WIDTH% - 1)
  y1% = RND(WIDTH% - 1)
  IF y1% <= x1% THEN
    y1% = bottom% - y1%
    x2% = x1%
    y2% = y1%
    IF RND(3) = 1 THEN
      x2% = x2% + 1
    ELSE
      y2% = y2% - 1
    ENDIF
    temp% = PIXCOL(x1% + LEFTAXIS%, y1%)
    IF (PIXCOL(x2% + LEFTAXIS%, y2%) EOR temp%) = eorval% THEN
      NOUPDATE
      PIXSET(x1% + LEFTAXIS%, y1%, PIXCOL(x2% + LEFTAXIS%, y2%))
      PIXSET(x2% + LEFTAXIS%, y2%, temp%)
      IF (x2% <> x1%) THEN
        PROCshowswap(x1%, temp%)
      ENDIF
      UPDATE
    ENDIF
  ENDIF
UNTIL 0 = 1
END

DEF PROCsetup
  CANVAS(0, 0, WIDTH% + LEFTAXIS% + RIGHTAXIS%, WIDTH% * 2 + TOPMARGIN% + GAP%)
  RESOLUTION(WIDTH% + LEFTAXIS% + RIGHTAXIS%, WIDTH% * 2 + TOPMARGIN% + GAP%)
  bottom% = WIDTH% * 2 + TOPMARGIN% + GAP% - 1
  boundary% = DIVMULT(WIDTH%, 1000, 707)
  eorval% = COLOUR1% EOR COLOUR2%
  COLOUR(COLOUR2%)
  SETXY(boundary% + 1 + LEFTAXIS%, bottom% - boundary%)
  DRAWXY(0, boundary%)
  THICKNESS(1)
  SETXY(boundary% + LEFTAXIS%, bottom% - boundary% - 10)
  DRAWXY(0, 5)
  SETXY(boundary% + LEFTAXIS%, TOPMARGIN%)
  DRAWXY(0, 5)
  COLOUR(COLOUR1%)
  SETXY(boundary% + LEFTAXIS% - 1, bottom% - boundary% - 10)
  DRAWXY(0, 5)
  SETXY(boundary% + LEFTAXIS% - 1, TOPMARGIN%)
  DRAWXY(0, 5)
  COLOUR(EDGECOL%)
  SETXY(LEFTAXIS% - 1, bottom%)
  DRAWXY(WIDTH% + 1, -WIDTH% - 1)
  COLOUR(EDGECOL%)
  DRAWXY(0, WIDTH% + 1)
  RECOLOUR(LEFTAXIS% + 2, bottom%, COLOUR1%)
  RECOLOUR(WIDTH% + LEFTAXIS% - 4, bottom%, COLOUR2%)
  FOR x% = 0 TO boundary% - 1
    num1%(x%) = x% + 1
  NEXT
  FOR x% = boundary% TO WIDTH% - 1
    num1%(x%) = 0
  NEXT
  COLOUR(BLACK)
  SETXY(0, 0)
  PRINT("Diffusion in a Tapering Tube", 2, WIDTH% / 10)
ENDPROC

DEF PROCdrawaxes
LOCAL n%
  FOR n% = -1 TO CONCWIDTH%
    PIXSET(LEFTAXIS% - 1, CONCBOTTOM% - n%, BLACK)
  NEXT
  FOR n% = 0 TO WIDTH%
    PIXSET(LEFTAXIS% + 100, TOPMARGIN% + n%, BLACK)
  NEXT
  FOR n% = 0 TO 100
    IF (n% < 31) OR (n% MOD 5 = 0) THEN
      PIXSET(LEFTAXIS% + n%, CONCBOTTOM% + 1, BLACK)
    ENDIF
    PIXSET(LEFTAXIS% + n%, TOPMARGIN% + 101, BLACK)
  NEXT
  DRAWXY(0, -CONCWIDTH%)
  COLOUR(CONCCOL%)
  SETXY(0, CONCBOTTOM% - CONCWIDTH% - WIDTH% / 15)
  PRINT("100%", 2, WIDTH% / 15)
  SETXY(0, CONCBOTTOM% - WIDTH% / 15)
  PRINT("  0%", 2, WIDTH% / 15)
  SETXY(0, TOPMARGIN% + WIDTH% * 11 / 10)
  PRINT("Red concentration", 2, WIDTH% / 12)
  COLOUR(MOLCOL%)
  SETXY(WIDTH% + LEFTAXIS% + 2, TOPMARGIN% + WIDTH% - boundary% + 1 - WIDTH% / 15)
  PRINT(STR$(boundary% - 1), 2, WIDTH% / 15)
  SETXY(WIDTH% + LEFTAXIS% + 2, TOPMARGIN% + WIDTH% - WIDTH% / 15)
  PRINT("  0", 2, WIDTH% / 15)
  SETXY(0, TOPMARGIN% + WIDTH% * 12 / 10)
  PRINT("Red molecule count", 2, WIDTH% / 12)
  COLOUR(BLACK)
  SETXY(0, TOPMARGIN% + WIDTH% * 13 / 10 + 5)
  PRINT("(at horizontal position", 2, WIDTH% / 15)
  SETXY(0, TOPMARGIN% + WIDTH% * 14 / 10 + 5)
  PRINT("in tapering tube)", 2, WIDTH% / 15)
ENDPROC

DEF PROCgraphit(x%, show%)
  IF show% THEN
    PIXSET(x% + LEFTAXIS%, TOPMARGIN% + WIDTH% - num1%(x%), MOLCOL%)
    PIXSET(x% + LEFTAXIS%, CONCBOTTOM% - DIVMULT(num1%(x%), x% + 1, CONCWIDTH%), CONCCOL%)
  ELSE
    PIXSET(x% + LEFTAXIS%, TOPMARGIN% + WIDTH% - num1%(x%), WHITE)
    PIXSET(x% + LEFTAXIS%, CONCBOTTOM% - DIVMULT(num1%(x%), x% + 1, CONCWIDTH%), WHITE)
  ENDIF
ENDPROC

DEF PROCshowswap(x%, origcol%)
  PROCgraphit(x%, FALSE)
  PROCgraphit(x% + 1, FALSE)
  IF origcol% = COLOUR1% THEN
    num1%(x%) = num1%(x%) - 1
    num1%(x% + 1) = num1%(x% + 1) + 1
  ELSE
    num1%(x%) = num1%(x%) + 1
    num1%(x% + 1) = num1%(x% + 1) - 1
  ENDIF
  PROCgraphit(x%, TRUE)
  PROCgraphit(x% + 1, TRUE)
ENDPROC
