CONST XLEFT% = 0
CONST XRIGHT% = 999
CONST YTOP% = 0
CONST YBOTTOM% = 999
CONST SCALEDOWN% = 4
CONST MAPPINGS% = 5
CONST TITLE$ = "Tree"
CONST LIVE% = GREEN
CONST COPIED% = EMERALD
CONST PAUSEVAL% = 2000
DIM mapxx%(MAPPINGS%)
DIM mapxy%(MAPPINGS%)
DIM mapxc%(MAPPINGS%)
DIM mapyx%(MAPPINGS%)
DIM mapyy%(MAPPINGS%)
DIM mapyc%(MAPPINGS%)
PROCsetmaps
PROCsetscale
CANVAS(xl%, yt%, xr% - xl% + 1, yb% - yt% + 1)
RESOLUTION(xr% - xl% + 1, yb% - yt% + 1)
PIXSET(1, 1, LIVE%)
iteration% = 0
WRITELN(TITLE$ + " by iterative mapping")
TIMESET(0)
REPEAT
  starttime% = TIME
  iteration% = iteration% + 1
  WRITELN("")
  WRITELN("iteration% " + STR$(iteration%) + ":")
  numborn% = 0
  FOR count% = 1 TO MAPPINGS%
    PROCdomap(count%)
  NEXT
  numkilled% = 0
  PROCcleanup
  WRITELN("  " + STR$(numborn%) + " pixels born " + STR$(numkilled%) + " pixels killed")
  WRITELN("  (took " + STR$((TIME - starttime%) / 1000) + " seconds - total " + STR$(TIME / 1000) + " seconds so far)")
  IF (numborn% + numkilled% > 0) AND (PAUSEVAL% >= 1000) THEN
    WRITE("  (now pausing " + QSTR$(PAUSEVAL%, 1000, 1) + " seconds to allow halting if desired ...)")
    PAUSE(PAUSEVAL%)
    WRITELN("")
  ENDIF
UNTIL numborn% + numkilled% = 0
WRITELN("FINISH - fixed point of iterative mapping has been reached")
END

DEF PROCsetmaps
  coeffdiv% = 1000
  mapxx%(1) = 195
  mapyx%(1) = 344
  mapxy%(1) = -488
  mapyy%(1) = 443
  mapxc%(1) = 443
  mapyc%(1) = 245
  mapxx%(2) = 462
  mapyx%(2) = -252
  mapxy%(2) = 414
  mapyy%(2) = 361
  mapxc%(2) = 251
  mapyc%(2) = 569
  mapxx%(3) = -58
  mapyx%(3) = 453
  mapxy%(3) = -70
  mapyy%(3) = -111
  mapxc%(3) = 598
  mapyc%(3) = 97
  mapxx%(4) = -35
  mapyx%(4) = -469
  mapxy%(4) = 70
  mapyy%(4) = -22
  mapxc%(4) = 488
  mapyc%(4) = 507
  mapxx%(5) = -637
  mapyx%(5) = 0
  mapxy%(5) = 0
  mapyy%(5) = 501
  mapxc%(5) = 856
  mapyc%(5) = 251
ENDPROC

DEF PROCsetscale
  LOCAL i%
  xl% = XLEFT% / SCALEDOWN%
  xr% = XRIGHT% / SCALEDOWN%
  yt% = YTOP% / SCALEDOWN%
  yb% = YBOTTOM% / SCALEDOWN%
  FOR i% = 1 TO MAPPINGS%
    mapxc%(i%) = mapxc%(i%) / SCALEDOWN%
    mapyc%(i%) = mapyc%(i%) / SCALEDOWN%
  NEXT
ENDPROC

DEF PROCmap (a%, x%, y%, RETURN newx%, RETURN newy%)
  newx% = DIVMULT(x%, coeffdiv%, mapxx%(a%)) + DIVMULT(y%, coeffdiv%, mapxy%(a%)) + mapxc%(a%)
  newy% = DIVMULT(x%, coeffdiv%, mapyx%(a%)) + DIVMULT(y%, coeffdiv%, mapyy%(a%)) + mapyc%(a%)
ENDPROC

DEF PROCdomap (mapnum%)
  LOCAL x%, y%, newx%, newy%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      IF PIXCOL(x%, y%) <> WHITE THEN
        PROCmap(mapnum%, x%, y%, newx%, newy%)
        IF PIXCOL(newx%, newy%) = WHITE THEN
          numborn% = numborn% + 1
        ENDIF
        PIXSET(newx%, newy%, COPIED%)
      ENDIF
    NEXT
  NEXT
  UPDATE
ENDPROC

DEF PROCcleanup
  LOCAL x%, y%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      IF PIXCOL(x%, y%) = LIVE% THEN
        PIXSET(x%, y%, WHITE)
        numkilled% = numkilled% + 1
      ELSE
        IF PIXCOL(x%, y%) = COPIED% THEN
          PIXSET(x%, y%, LIVE%)
        ENDIF
      ENDIF
    NEXT
  NEXT
  UPDATE
ENDPROC
