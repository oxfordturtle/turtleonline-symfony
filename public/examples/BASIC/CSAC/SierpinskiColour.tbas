CONST XLEFT% = 0
CONST XRIGHT% = 999
CONST YTOP% = 0
CONST YBOTTOM% = 999
CONST SCALEDOWN% = 4
CONST MAPPINGS% = 3
CONST TITLE$ = "Sierpinski triangle"
CONST GRIDLINES% = 5
CONST GRIDTHICK% = 64
DIM mapxx%(MAPPINGS%)
DIM mapxy%(MAPPINGS%)
DIM mapxc%(MAPPINGS%)
DIM mapyx%(MAPPINGS%)
DIM mapyy%(MAPPINGS%)
DIM mapyc%(MAPPINGS%)
PROCsetmaps
PROCsetscale
CANVAS(xl%, yt%, xr% - xl% + 1, yb% - yt% + 1)
RESOLUTION(xr% - xl% + 1, yb% - yt% + 1)
PROCbackground
WRITELN(TITLE$ + " MAPPINGS% in colour")
FOR count% = 1 TO MAPPINGS%
  PROCdomap(count%)
NEXT
END

DEF PROCsetmaps
  coeffdiv% = 100
  mapxx%(1) = 50
  mapyx%(1) = 0
  mapxy%(1) = 0
  mapyy%(1) = 50
  mapxc%(1) = 250
  mapyc%(1) = 0
  mapxx%(2) = 50
  mapyx%(2) = 0
  mapxy%(2) = 0
  mapyy%(2) = 50
  mapxc%(2) = 0
  mapyc%(2) = 500
  mapxx%(3) = 50
  mapyx%(3) = 0
  mapxy%(3) = 0
  mapyy%(3) = 50
  mapxc%(3) = 500
  mapyc%(3) = 500
ENDPROC

DEF PROCsetscale
  LOCAL i%
  xl% = XLEFT% / SCALEDOWN%
  xr% = XRIGHT% / SCALEDOWN%
  yt% = YTOP% / SCALEDOWN%
  yb% = YBOTTOM% / SCALEDOWN%
  FOR i% = 1 TO MAPPINGS%
    mapxc%(i%) = mapxc%(i%) / SCALEDOWN%
    mapyc%(i%) = mapyc%(i%) / SCALEDOWN%
  NEXT
  gt% = GRIDTHICK% / SCALEDOWN%
ENDPROC

DEF PROCbackground
  LOCAL x%, y%, cx%, cy%, r%, g%, b%
  THICKNESS(gt%)
  COLOUR(DARKGREY)
  FOR x% = 1 TO GRIDLINES% - 1
    SETXY(xl% + DIVMULT(xr% - xl%, GRIDLINES%, x%), yt%)
    DRAWXY(0, yb% - yt%)
  NEXT
  FOR y% = 1 TO GRIDLINES% - 1
    SETXY(xl% + gt% / 2, yt% + DIVMULT(yb% - yt%, GRIDLINES%, y%))
    DRAWXY(xr% - xl% - gt%, 0)
  NEXT
  COLOUR(BLACK)
  SETXY(xl%, yt%)
  DRAWXY(xr% - xl%, 0)
  DRAWXY(0, yb% - yt%)
  DRAWXY(xl% - xr%, 0)
  DRAWXY(0, yt% - yb%)
  FOR x% = 0 TO GRIDLINES% - 1
    FOR y% = 0 TO GRIDLINES% - 1
      cx% = xl% + DIVMULT(xr% - xl%, GRIDLINES%, x%) + gt%
      cy% = yt% + DIVMULT(yb% - yt%, GRIDLINES%, y%) + gt%
      r% = DIVMULT(255, GRIDLINES% - 1, x%)
      g% = DIVMULT(255, GRIDLINES% - 1, y%)
      b% = DIVMULT(255, 2 * GRIDLINES% - 2, 2 * GRIDLINES% - 2 - x% - y%)
      RECOLOUR(cx%, cy%, (r% * &10000) + (g% * &100) + (b% * &1))
    NEXT
  NEXT
ENDPROC

DEF PROCmap (a%, x%, y%, RETURN newx%, RETURN newy%)
  newx% = DIVMULT(x%, coeffdiv%, mapxx%(a%)) + DIVMULT(y%, coeffdiv%, mapxy%(a%)) + mapxc%(a%)
  newy% = DIVMULT(x%, coeffdiv%, mapyx%(a%)) + DIVMULT(y%, coeffdiv%, mapyy%(a%)) + mapyc%(a%)
ENDPROC

DEF PROCdomap (mapnum%)
  LOCAL x%, y%, newx%, newy%
  NOUPDATE
  FOR y% = yt% TO yb%
    FOR x% = xl% TO xr%
      PROCmap(mapnum%, x%, y%, newx%, newy%)
      PIXSET(newx%, newy%, PIXCOL(x%, y%))
    NEXT
  NEXT
  UPDATE
ENDPROC
