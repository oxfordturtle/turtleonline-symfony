REM draw a face of radius 400
PROCface(400)
END

REM draws a face of the given radius
DEF PROCface(radius%)
  REM avoid drawing lines as Turtle moves
  PENUP
  REM draw head with blot of given radius
  COLOUR(PEACH)
  BLOT(radius%)
  REM draw mouth using overlapping blots
  COLOUR(DARKRED)
  BLOT(radius% * 7 / 10)
  FORWARD(radius% * 3 / 20)
  COLOUR(PEACH)
  BLOT(radius% * 3 / 4)
  REM draw blue nose
  BACK(radius% * 3 / 10)
  COLOUR(ROYAL)
  BLOT(radius% * 3 / 20)
  REM move to left eye position
  FORWARD(radius% * 9 / 20)
  LEFT(90)
  REM Turtle is now pointing leftwards
  FORWARD(radius% * 2 / 5)
  REM draw left eye
  PROCeye(radius%)
  REM move to right eye position
  BACK(radius% * 4 / 5)
  REM draw right eye
  PROCeye(radius%)
ENDPROC

REM draws an eye of appropriate size
DEF PROCeye(faceRadius%)
  COLOUR(WHITE)
  ELLBLOT(faceRadius% * 3 / 20, faceRadius% / 10)
  COLOUR(EMERALD)
  BLOT(faceRadius% * 9 / 100)
  COLOUR(BLACK)
  BLOT(faceRadius% / 25)
ENDPROC
