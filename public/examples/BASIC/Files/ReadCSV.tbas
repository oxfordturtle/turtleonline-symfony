REM ReadCSV
CONST cols% = 10
CONST rows% = 10
CONST fname$ = "SaveCSV.csv"
STRINGSIZE 64
DIM value%(cols%, rows%)
PROCsetup
PROCdrawgrid
PROClabelgrid
PROCreaddata
PROCdisplay
WRITELN("File " + fname$ + " has been read")
END

DEF PROCsetup
  colwidth% = 1000 DIV (cols% + 1)
  rowheight% = 900 DIV (rows% + 1)
  xmargin% = colwidth% * 3 / 7
  ymargin% = rowheight% / 3
  fontsize% = colwidth% / 4
ENDPROC

DEF PROCdrawgrid
  LOCAL i%, j%
  FOR i% = 0 TO cols% + 1
    IF (i% = 0) OR (i% = 1) OR (i% = cols% + 1) THEN
      THICKNESS(10)
    ELSE
      THICKNESS(2)
    ENDIF
    SETXY(5 + i% * colwidth%, 5)
    DRAWXY(0, 890)
  NEXT
  FOR j% = 0 TO rows% + 1
    IF (j% = 0) OR (j% = 1) OR (j% = rows% + 1) THEN
      THICKNESS(10)
    ELSE
      THICKNESS(2)
    ENDIF
    SETXY(5, 5 + j% * rowheight%)
    DRAWXY(995, 0)
  NEXT
ENDPROC

DEF PROClabelgrid
  LOCAL i%, j%
  SETXY(colwidth% - 25, 5)
  PRINT("x", 0, 30)
  SETXY(20, rowheight% - 45)
  PRINT("y", 0, 30)
  SETXY(300, 925)
  PRINT("Table read from file", 0, 40)
  FOR i% = 1 TO cols%
    SETXY(i% * colwidth% + xmargin%, ymargin%)
    PRINT(STR$(i%), 0, fontsize%)
  NEXT
  FOR j% = 1 TO rows%
    SETXY(xmargin%, j% * rowheight% + ymargin%)
    PRINT(STR$(j%), 0, fontsize%)
  NEXT
ENDPROC

DEF PROCdisplay
  LOCAL i%, j%
  FOR i% = 1 TO cols%
    FOR j% = 1 TO rows%
      SETXY(i% * colwidth% + xmargin%, j% * rowheight% + ymargin%)
      PRINT(STR$(value%(i%, j%)), 0, fontsize%)
    NEXT
  NEXT
ENDPROC

DEF FNnextbit$(RETURN s$)
  LOCAL posn%, result$
  posn% = INSTR(s$, ",")
  IF posn% = 0 THEN
    result$ = s$
    s$ = ""
  ELSE
    result$ = MID$(s$, 1, posn% - 1)
    s$ = DEL$(s$, 1, posn%)
  ENDIF
=result$

DEF PROCreaddata
  LOCAL handle%, i%, j%, thisline$, thisbit$
  i% = 0
  handle% = OPENIN(fname$)
  WHILE NOT(EOF#(handle%))
    INC(i%)
    thisline$ = FREADLN#(handle%)
    j% = 0
    WHILE thisline$ <> ""
      INC(j%)
      thisbit$ = FNnextbit$(thisline$)
      value%(i%, j%) = VAL(thisbit$)
    ENDWHILE
  ENDWHILE
  CLOSE#(handle%)
ENDPROC
