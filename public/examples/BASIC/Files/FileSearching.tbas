REM set up the output
OUTPUT(TRUE, CREAM, TRUE)
WRITELN("Turtle directories (up to first level) and their files ...")
WRITELN("")
REM show files in the base directory
PROCshowFiles("")
REM look for subdirectories, and show their files
dirHandle% = 0
dirName$ = FINDDIR("*.*", dirHandle%)
WHILE dirName$ <> ""
  PROCshowFiles(dirName$)
  dirName$ = FINDNEXT(dirHandle%)
ENDWHILE
END

REM lists all files in a directory
DEF PROCshowFiles(dirName$)
  LOCAL fileHandle%, fileName$
  IF dirName$ = "" THEN
    WRITELN("Base directory:")
  ELSE
    WRITELN("Directory """ + dirName$ + """:")
  ENDIF
  CHDIR(dirName$)
  fileHandle% = 0
  fileName$ = FINDFIRST("*.*", fileHandle%)
  IF fileName$ = "" THEN
    WRITELN("  <no files>")
  ELSE
    WHILE fileName$ <> ""
      WRITELN("  " + fileName$)
      fileName$ = FINDNEXT(fileHandle%)
    ENDWHILE
  ENDIF
  WRITELN("")
ENDPROC
