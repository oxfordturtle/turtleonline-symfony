CONST SUNRADIUS% = 50000
DIM radius%(7)
DIM distanceFromSun%(7)
DIM design%(7)
DIM rotation%(7)
DIM speed%(7)
PROCsetup
REM multiply canvas dimensions by 1000
ANGLES(360000)
CANVAS(0, 0, 1000000, 1000000)
PENUP
WHILE TRUE
  NOUPDATE
  REM rub out the previous frame
  BLANK(BLACK)
  REM draw the sun
  HOME
  COLOUR(YELLOW)
  BLOT(SUNRADIUS%)
  REM draw each planet
  FOR i% = 0 TO 7
    HOME
    REM draw a white circle for the orbit
    COLOUR(WHITE)
    CIRCLE(distanceFromSun%(i%) + SUNRADIUS%)
    REM move to the next position and draw the planet
    RIGHT(rotation%(i%))
    FORWARD(distanceFromSun%(i%) + SUNRADIUS%)
    rotation%(i%) = (rotation%(i%) + speed%(i%)) MOD 360000
    COLOUR(design%(i%))
    IF i% < 4 THEN
      BLOT(radius%(i%) * 2) REM make inner planets double scale
    ELSE
      BLOT(radius%(i%) DIV 2) REM make outer planets half scale
    ENDIF
  NEXT
  UPDATE
ENDWHILE
END

DEF PROCsetup
  REM mercury
  radius%(0) = 2433
  distanceFromSun%(0) = 5795
  speed%(0) = 1607
  design%(0) = WHITE
  REM venus
  radius%(1) = 6053
  distanceFromSun%(1) = 10811
  speed%(1) = 1174
  design%(1) = OCHRE
  REM earth
  radius%(2) = 6371
  distanceFromSun%(2) = 14957
  speed%(2) = 1000
  design%(2) = BLUE
  REM mars
  radius%(3) = 3380
  distanceFromSun%(3) = 22784
  speed%(3) = 802
  design%(3) = ORANGERED
  REM jupiter
  radius%(4) = 71492
  distanceFromSun%(4) = 77814
  speed%(4) = 434
  design%(4) = LIGHTRED
  REM saturn
  radius%(5) = 58219
  distanceFromSun%(5) = 142700
  speed%(5) = 323
  design%(5) = CREAM
  REM uranus
  radius%(6) = 23470
  distanceFromSun%(6) = 287030
  speed%(6) = 182
  design%(6) = CYAN
  REM neptune
  radius%(7) = 23716
  distanceFromSun%(7) = 449990
  speed%(7) = 159
  design%(7) = ROYAL
ENDPROC
