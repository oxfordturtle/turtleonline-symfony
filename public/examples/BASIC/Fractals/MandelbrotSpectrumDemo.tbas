CONST MAXCOL% = 40
CONST SPECTRUM% = 7
DIM spectcol%(SPECTRUM%)
DIM boundary%(SPECTRUM%)
PROCstartprompt
PROCcolsetup
xstart% = DIVMULT(xcentre%, 1000000, scale%) - pixels% / 2
ystart% = DIVMULT(ycentre%, 1000000, scale%) - pixels% / 2
xfinish% = xstart% + pixels% - 1
yfinish% = ystart% + pixels% - 1
WRITELN("Mandelbrot will be plotted over the following real range:")
WRITE(QSTR$(xstart%, scale%, 4) + " < x < " + QSTR$(xfinish%, scale%, 4) + "    ")
WRITELN(QSTR$(ystart%, scale%, 4) + " < y < " + QSTR$(yfinish%, scale%, 4))
WRITELN("Scaling factor: " + STR$(scale%) + "    Image resolution: " + STR$(pixels%) + "x" + STR$(pixels%))
CANVAS(xstart%, ystart%, pixels%, pixels%)
RESOLUTION(pixels%, pixels%)
TIMESET(0)
FOR a% = xstart% TO xfinish%
  NOUPDATE
  FOR b% = ystart% TO yfinish%
    x% = a%
    y% = b%
    iterations% = 0
    WHILE (HYPOT(x%, y%, 1) < 2 * scale%) AND (iterations% <= MAXCOL%)
      temp% = DIVMULT(x% + y%, scale%, x% - y%)
      y% = DIVMULT(2 * x%, scale%, y%) + b%
      x% = temp% + a%
      iterations% = iterations% + 1
    ENDWHILE
    IF iterations% > MAXCOL% THEN
      PIXSET(a%, b%, BLACK)
    ELSE
      PIXSET(a%, b%, FNmixcolour(iterations%))
    ENDIF
  NEXT
  UPDATE
NEXT
WRITELN("Time taken: " + QSTR$(TIME, 1000, 1) + " seconds.")
END

DEF PROCstartprompt
  OUTPUT(TRUE, CREAM, TRUE)
  KEYECHO(FALSE)
  WRITELN("MANDELBROT SET DISPLAY PROGRAM")
  WRITELN("")
  WRITE("Select Whole set, or Zoom on mini ""lake"" at -0.1592,-1.0330 (W/Z)")
  REPEAT
  UNTIL (ABS(?key) = \w) OR (ABS(?key) = \z)
  WRITELN(CHR$(ABS(?key)))
  IF ABS(?key) = \w THEN
    xcentre% = -500000
    ycentre% = 0
  ELSE
    xcentre% = -159200
    ycentre% = -1033000
  ENDIF
  WRITELN("")
  IF ycentre% = 0 THEN
    WRITE("Select Fast/Medium/Slow, giving resolution 300/750/1500: (F/M/S) ")
  ELSE
    WRITE("Select Fast/Medium/Slow, giving resolution 300/600/1200: (F/M/S) ")
  ENDIF
  REPEAT
  UNTIL (ABS(?key) = \f) OR (ABS(?key) = \m) OR (ABS(?key) = \s)
  WRITELN(CHR$(ABS(?key)))
  IF ycentre% = 0 THEN
    IF ABS(?key) = \f THEN
      scale% = 100
    ELSE
      IF ABS(?key) = \m THEN
        scale% = 250
      ELSE
        scale% = 500
      ENDIF
    ENDIF
    pixels% = scale% * 3
  ELSE
    IF ABS(?key) = \f THEN
      scale% = 10000
    ELSE
      IF ABS(?key) = \m THEN
        scale% = 20000
      ELSE
        scale% = 40000
      ENDIF
    ENDIF
    pixels% = scale% / 100 * 3
  ENDIF
  OUTPUT(FALSE, WHITE, FALSE)
ENDPROC

DEF PROCcolsetup
  spectcol%(0) = VIOLET
  spectcol%(1) = BLUE
  spectcol%(2) = CYAN
  spectcol%(3) = LIME
  spectcol%(4) = YELLOW
  spectcol%(5) = ORANGE
  spectcol%(6) = RED
  spectcol%(7) = VIOLET
  FOR n% = 0 TO SPECTRUM%
    boundary%(n%) = DIVMULT(MAXCOL%, SPECTRUM%, n%)
  NEXT
ENDPROC

DEF FNmixcolour(n%)
  col2% = 0
  REPEAT
    col2% = col2% + 1
  UNTIL (boundary%(col2%) >= n%) OR (col2% = SPECTRUM%)
  col1% = col2% - 1
= MIXCOLS(spectcol%(col1%), spectcol%(col2%), boundary%(col2%) - n%, n% - boundary%(col1%))
