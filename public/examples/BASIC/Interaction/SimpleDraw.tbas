REM draw palette
PROCpalette
REM return turtle to centre of canvas
HOME
THICKNESS(10)
BLOT(2)
REM repeatedly...
REPEAT
  REM wait for left or right mouse click...
  REPEAT
  UNTIL (?LMOUSE > 0) OR (?RMOUSE > 0)
  REM if cursor is within the palette area...
  IF ?MOUSEY > 900 THEN
    REM set the colour
    COLOUR(RGB(?MOUSEX DIV 100 + 1))
  ELSE
    REM if left button pressed...
    IF ?LMOUSE > 0 THEN
      REM move the turtle (drawing) to the cursor's location
      DRAWXY(?MOUSEX - turtx%, ?MOUSEY - turty%)
    REM otherwise (i.e. right button pressed)...
    ELSE
      REM move the turtle (not drawing) the the cursor's location
      SETXY(?MOUSEX, ?MOUSEY)
    ENDIF
  ENDIF
UNTIL FALSE
END

REM draws the palette of colours
DEF PROCpalette
  LOCAL colr%
  FOR colr% = 1 TO 10
    SETXY(colr% * 100 - 50, 950)
    COLOUR(RGB(colr%))
    BLOT(50)
  NEXT
ENDPROC
